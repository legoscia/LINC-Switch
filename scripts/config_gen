#!/usr/bin/env escript

main([]) ->
    usage();
main(Args) ->
    try parse_args(Args, []) of
        ParsedConfig ->
            {output_file, OutputFile} = lists:keyfind(output_file, 1,
                                                      ParsedConfig),
            SysConfig = generate_sys_config(ParsedConfig),
            ok = file:write_file(OutputFile, io_lib:fwrite("~p.~n", [SysConfig]))
    catch
        _:required_argument_missing ->
            io:format("Required argument missing.~n"),
            usage();
        _:_ ->
            io:format("Invalid argument.~n"),
            usage()
    end.

usage() ->
    ScriptName = escript:script_name(),
    io:format("~s~n~n"
              "Example usage:~n ~s~n",
              [usage_pattern(ScriptName), usage_example(ScriptName)]).

usage_pattern(ScriptName) ->
    ScriptName ++ " [-s LOGICAL_SWITCH_ID [INTERFACE_NAME]]"
        " -c [[tcp|tls]:CONTROLLER_ADDRES:CONTROLLER_PORT]"
        " [-l CONTROLLERS_LISTENER_ADDRESS:PORT]"
        " [-b BACKEND]"
        " -o OUTPUT_FILE".

usage_example(ScriptName) ->
    ScriptName ++ "-s 0 tap0 tap1 -c tcp:10.48.11.5:6653 tls:10.48.11.6:5533 "
        "-o ../rel/files/sys.config".

parse_args([], Config) ->
    [lists:keymember(RequiredArgument, 1, Config) orelse error(required_argument_missing)
     || RequiredArgument <- [switches, controllers, output_file]],
    Config;
parse_args(["-s", LogicalSwitch | Rest], Config) ->
    {Interfaces, NewRest} = parse_interfaces(Rest),
    Switches = case lists:keyfind(switches, 1, Config) of
                   false ->
                       [];
                   {switches, List} ->
                       List
               end,
    parse_args(NewRest,
               lists:keystore(
                 switches,
                 1,
                 Config,
                 {switches, [{LogicalSwitch, Interfaces} | Switches]}));
parse_args(["-o", Outputfile | Rest], Config) ->
    parse_args(Rest, [{output_file, Outputfile} | Config]);
parse_args(["-c" | Rest], Config) ->
    {Controllers, NewRest} = parse_controllers(Rest),
    parse_args(NewRest, [{controllers, Controllers} | Config]);
parse_args(["-l" | Rest], Config) ->
    {ControllersListener, NewRest} = parse_controllers_listener(Rest),
    parse_args(NewRest, [{controllers_listener, ControllersListener} | Config]);
parse_args(["-b", Backend | Rest], Config) ->
    parse_args(Rest, [{backend, Backend} | Config]).


parse_interfaces(Args) ->
    split_to_next_option(Args).

split_to_next_option(Args) ->
    lists:splitwith(fun(Element) ->
                            Element =/= "-s"
                                andalso Element =/= "-o"
                                andalso Element =/= "-c"
                                andalso Element =/= "-l"
                                andalso Element =/= "-b"
                    end, Args).

parse_controllers(Args) ->
    {Controllers, Rest} = split_to_next_option(Args),
    {lists:foldl(fun(Controller, Acc) ->
                         [list_to_tuple(string:tokens(Controller, ":")) | Acc]
                 end, [], Controllers),
     Rest}.

parse_controllers_listener(Args) ->
    {[ControllersListener], Rest} = split_to_next_option(Args),
    {list_to_tuple(string:tokens(ControllersListener, ":")), Rest}.

generate_sys_config(ParsedConfig) ->
    case lists:keyfind(backend, 1, ParsedConfig) of
        {backend, BackendS} ->
            Backend = list_to_atom(BackendS);
        false ->
            Backend = linc_us4
    end,
    PortsNames = ports_names(ParsedConfig),
    PortsMap = lists:zip(PortsNames, lists:seq(1, length(PortsNames))),
    {ExtraOptions, ExtraPorts} =
        extra_options_ports(Backend, length(PortsNames) + 1, ParsedConfig),
    [
     {linc,
      [
       {of_config, enabled},
       {sync_routing, true},
       {capable_switch_ports, generate_ports(PortsMap) ++ ExtraPorts},
       {capable_switch_queues, []},
       {logical_switches,
        generate_logical_switches(ParsedConfig, Backend, PortsMap, ExtraOptions)}
      ] ++ ExtraOptions},
     {enetconf,
      [
       {capabilities, [{base, {1, 0}},
                       {base, {1, 1}},
                       {startup, {1, 0}},
                       {'writable-running', {1, 0}}]},
       {callback_module, linc_ofconfig},
       {sshd_ip, any},
       {sshd_port, 1830},
       {sshd_user_passwords,
        [
         {"linc", "linc"}
        ]}
      ]},
     {lager,
      [
       {handlers,
        [
         {lager_console_backend, debug},
         {lager_file_backend,
          [
           {"log/error.log", error, 10485760, "$D0", 5},
           {"log/console.log", info, 10485760, "$D0", 5}
          ]}
        ]}
      ]},
     {sasl,
      [
       {sasl_error_logger, {file, "log/sasl-error.log"}},
       {errlog_type, error},
       {error_logger_mf_dir, "log/sasl"},      % Log directory
       {error_logger_mf_maxbytes, 10485760},   % 10 MB max file size
       {error_logger_mf_maxfiles, 5}           % 5 files max
      ]},
     {sync,
      [
       {excluded_modules, [procket]}
      ]}
    ].

ports_names(ParsedConfig) ->
    {switches, Switches} = lists:keyfind(switches, 1, ParsedConfig),
    lists:foldl(fun({_, Ports}, Acc) ->
                        Ports ++ Acc
                end, [], Switches).

extra_options_ports(linc_us4_oe, MinPortNo, ParsedConfig) ->
    {switches, Switches} = lists:keyfind(switches, 1, ParsedConfig),
    OpticalPorts =
        [{list_to_integer(SwitchId1), list_to_integer(SwitchId2)} ||
            {SwitchId1, _} <- Switches, {SwitchId2, _} <- Switches,
            SwitchId1 =/= SwitchId2],
    OpticalPortsNumbered =
        lists:zip(lists:seq(MinPortNo, MinPortNo + length(OpticalPorts) - 1),
                  OpticalPorts),
    OpticalPortLinks =
        [case lists:keyfind({SwitchId2, SwitchId1}, 2, OpticalPortsNumbered) of
             {PortNo2, {SwitchId2, SwitchId1}} ->
                 {{SwitchId1, PortNo1}, {SwitchId2, PortNo2}}
         end
         || {PortNo1, {SwitchId1, SwitchId2}} <- OpticalPortsNumbered,
            SwitchId1 < SwitchId2],
    {
      [{optical_links, OpticalPortLinks}],
      [{port, PortNo, [{type, optical}, {interface, ""}]}
       || {PortNo, _} <- OpticalPortsNumbered]
    };
extra_options_ports(_Backend, _MinPortNo, _ParsedConfig) ->
    {[], []}.

generate_ports(PortsMap) ->
    [{port, PortNo, [{interface, PortName}]} || {PortName, PortNo} <- PortsMap].

generate_logical_switches(ParsedConfig, Backend, PortsMap, ExtraOptions) ->
    {switches, Switches} = lists:keyfind(switches, 1, ParsedConfig),
    {controllers, Controllers} = lists:keyfind(controllers, 1, ParsedConfig),
    [
     {switch, list_to_integer(SwitchId),
      [
       {backend, Backend},
       {controllers, generate_logical_switch_controllers(SwitchId, Controllers)},
       {controllers_listener,
        generate_logical_switch_controllers_listener(ParsedConfig)},
       {queues_status, disabled},
       {ports, generate_logical_switch_ports(PortsNames, PortsMap)
        ++ extra_logical_switch_ports(Backend, list_to_integer(SwitchId), ExtraOptions)}
      ]}
     || {SwitchId, PortsNames} <- Switches].

generate_logical_switch_controllers(SwitchId, Controllers) ->
    lists:foldl(fun({Protocol, HostOrIP, Port}, Acc) ->
                        [{"Switch" ++ SwitchId ++ "-Controller",
                          HostOrIP,
                          list_to_integer(Port),
                          list_to_atom(Protocol)} | Acc]
                end, [], Controllers).

generate_logical_switch_controllers_listener(ParsedConfig) ->
    case lists:keyfind(controllers_listener, 1, ParsedConfig) of
        {controllers_listener, {IP, Port}} ->
            {IP, list_to_integer(Port), tcp};
        false ->
            disabled
    end.

generate_logical_switch_ports(PortsNames, PortsMap) ->
    [{port, element(2, lists:keyfind(PortName, 1, PortsMap)), [{queues, []}]}
     || PortName <- PortsNames].

extra_logical_switch_ports(linc_us4_oe, SwitchId, ExtraOptions) ->
    {optical_links, OpticalLinks} = lists:keyfind(optical_links, 1, ExtraOptions),
    [{port, PortNo, [{queues, []}]}
     || OpticalLink <- OpticalLinks,
        {TheSwitchId, PortNo} <- tuple_to_list(OpticalLink),
        TheSwitchId =:= SwitchId];
extra_logical_switch_ports(_Backend, _SwitchId, _ExtraOptions) ->
    [].
